10 REM GAME
10 _SPRDISABLE:DEFINT A-Z:COLOR 15,1,1:SCREEN 2,2,0
20 A=0:I=0:J=0:K=0:O=0:EX=0:EY=0
30 REM BASIC SPRITE SUBSYSTEM VARIABLES
30 SU=0:DIM SA(3,31):DIM AD(3)
40 REM PLAYER VARIABLES
40 DIM PG(2,1):DIM AR(1):DIM AL(1):X=0:Y=0:VY=0:AC=0:F=0
50 REM FOX 1 VARIABLES
50 FX=0:FY=0:FS=0:DIM FG(2,1):DIM FR(1):DIM FL(1)
60 REM FOX 2 VARIABLES
60 GX=0:GY=0:GS=0:DIM GG(2,1):DIM GR(1):DIM GL(1)
70 REM SNAKE 1 VARIABLES
70 SX=0:SY=0:SS=0:DIM SG(2,1):DIM SR(1):DIM SL(1)
200 REM PLATFORM LOCATIONS
200 DIM PL(24,31)
210 _MAXAUTOSGAMS(0):_MAXANIMSPRS(0):_MAXANIMDEFS(0):_MAXANIMITEMS(0)
220 _MAXANIMITEMS(34):_MAXANIMDEFS(13):_MAXANIMSPRS(19):_MAXAUTOSGAMS(3)

1000 BLOAD"LVL.SC2",S
1490 REM PLATFORM
1490 RESTORE 30000:FOR I=0 TO 15:READ A:POKE &HB000+I,A:NEXT:_MEMCPY(&HB000,&H7000,16)
1560 REM DEFINE SPRITE GROUP FOR PLAYER
1560 PG(0,0)=0:PG(1,0)=0:PG(2,0)=0
1570 PG(0,1)=1:PG(1,1)=0:PG(2,1)=0
1590 REM DEFINE SPRITE GROUP FOR FOX 1
1590 FG(0,0)=2:FG(1,0)=0:FG(2,0)=0:FG(0,1)=3:FG(1,1)=0:FG(2,1)=0
1591 REM DEFINE SPRITE GROUP FOR FOX 2
1591 GG(0,0)=4:GG(1,0)=0:GG(2,0)=0:GG(0,1)=5:GG(1,1)=0:GG(2,1)=0
1592 REM DEFINED SPRITE GROUP FOR SNAKE 1
1592 SG(0,0)=6:SG(1,0)=0:SG(2,0)=0:SG(0,1)=7:SG(1,1)=0:SG(2,1)=0
1600 REM DEFINE ANIMATION ITEMS - PATTERN AND COLOR CHANGES FOR SPRITES
1600 RESTORE 50000:READ A:FOR I=0 TO A-1:FOR J=0 TO 2:READ AD(J):NEXT J:_ANIMITEMPAT(I,AD(0),AD(1),AD(2)):NEXT I
1605 REM DEFINE ANIMATION ITEMS - PATTERN DEFINITION CHANGES
1605 READ K:FOR I=A TO A+K-1:FOR J=0 TO 1:READ AD(J):NEXT J:_ANIMITEMPTR(I,AD(0),AD(1)):NEXT I
1610 REM DEFINE ANIMATIONS
1610 READ A:FOR I=0 TO A-1:READ K:FOR J=0 TO K-1:READ AD(J):NEXT J:_ANIMDEF(I,K,AD):NEXT I
1620 REM DEFINE SPRITE ANIMATIONS
1620 READ A:FOR I=0 TO A-1:FOR J=0 TO 2:READ AD(J):NEXT J:_ANIMSPRITE(I,AD(0),AD(1),AD(2)):NEXT I
1630 REM DEFINE CHARACTER ANIMATIONS
1630 READ K:FOR I=A TO A+K-1:FOR J=0 TO 2:READ AD(J):NEXT J:_ANIMCHAR(I,AD(0),AD(1),AD(2)):NEXT I
1710 BLOAD"LEVEL.BIN":_MEMCPY(&HB000,VARPTR(PL(0,0)),1600)
1980 _SPRENABLE (SA,SU,1):FOR I=0 TO 31:_SPRSET (I,256,256,I,4):PL(24,I)=1:NEXT
1990 FX=200:FY=176:FS=2:GX=112:GY=56:GS=1:SX=56:SY=25:SS=1
2000 X=120:Y=112*16:AC=4:VY=0
2010 SA(2,0)=0:SA(3,0)=1
2011 SA(2,1)=1:SA(3,1)=15
2014 SA(2,2)=12:SA(3,2)=1
2015 SA(2,3)=15:SA(3,3)=11
2016 SA(2,4)=12:SA(3,4)=1
2017 SA(2,5)=15:SA(3,5)=11
2018 SA(2,6)=24:SA(3,6)=1
2019 SA(2,7)=25:SA(3,7)=13
2050 _ANIMSTART(16):_ANIMSTART(17):_ANIMSTART(18)
2060 ON STOP GOSUB 7000:STOP ON
2070 ON ERROR GOTO 7000
2080 REM ANIMATION GROUPS
2080 REM PLAYER RIGHT
2080 AR(0)=0:AR(1)=1
2090 REM PLAYER LEFT
2090 AL(0)=2:AL(1)=3
2100 REM FOX 1 RIGHT
2100 FR(0)=4:FR(1)=5
2110 REM FOX 1 LEFT
2110 FL(0)=6:FL(1)=7
2120 REM FOX 2 RIGHT
2120 GR(0)=8:GR(1)=9
2130 REM FOX 1 LEFT
2130 GL(0)=10:GL(1)=11
2140 REM SNAKE 1 RIGHT
2140 SR(0)=12:SR(1)=13
2150 REM SNAKE 1 LEFT
2150 SL(0)=14:SL(1)=15
2160 _SPRGRPMOV(X,Y\16,2,PG):_SPRGRPMOV(FX,FY,2,FG):_SPRGRPMOV(GX,GY,2,GG):_SPRGRPMOV(SX,SY,2,SG):SU=1
2170 _AUTOSGAMDEF(0,FX,FY,0,240,1,0,2,2,FG,2,FL,FR):_AUTOSGAMDEF(1,GX,GY,112,144,1,0,4,2,GG,2,GL,GR)
2180 _AUTOSGAMDEF(2,SX,SY,56,96,1,0,4,2,SG,2,SL,SR)
2190 _AUTOSGAMSTART(0):_AUTOSGAMSTART(1):_AUTOSGAMSTART(2)
2200 ON STRIG GOSUB 5000:STRIG(0) ON

3000 I=(X+4)\8:J=Y\128+(Y<0):IF J<-2 OR VY<0 GOTO 3525
3511 IF VY>0 GOTO 3520 ELSE IF PL(J+2,I)>0 OR PL(J+2,I+1)>0 GOTO 3530
3520 K=(Y+VY)\128:IF K=J GOTO 3525 ELSE IF PL(K+2,I)>0 OR PL(K+2,I+1)>0 THEN VY=0:F=0:Y=K*128:GOTO 3530 
3525 Y=Y+VY:VY=VY+AC
3530 ON STICK(0)+1 GOTO 3580,3580,3540,3540,3540,3580,3560,3560,3560
3540 X=X+2+(VY<>0):IF X>240 THEN X=240
3550 _SGAM(X,Y\16,2,PG,2,AR):GOTO 3000
3560 X=X-2-(VY<>0):IF X<0 THEN X=0
3570 _SGAM(X,Y\16,2,PG,2,AL):GOTO 3000
3580 _SPRGRPMOV(X,Y\16,2,PG):SU=1:GOTO 3000

5000 REM STRIG(0) SUBROUTINE
5000 IF F=0 THEN F=1:VY=-55
5010 RETURN

7000 _ANIMSTOP(16):_ANIMSTOP(17):_ANIMSTOP(18)
7010 _AUTOSGAMSTOP(0):_AUTOSGAMSTOP(1):_AUTOSGAMSTOP(2)
7020 _SPRDISABLE:_SNDPLYOFF
7030 SCREEN 0
7040 END

30000 REM PLATFORM ANIMATION DEFINITION
30000 DATA &H66,&HCC,&H99,&H33,&H66,&HCC,&H99,&H33
30010 DATA &H99,&H33,&H66,&HCC,&H99,&H33,&H66,&HCC

50000 REM PATTERN AND COLOR CHANGE ANIMATIONS - NUMBER
50000 DATA 32
50010 REM ANIMATION - DEFINITIONS (DURATION, PATTERN, COLOR)
50010 REM PLAYER RIGHT DIRECTION FIRST
50010 REM BLACK
50010 DATA 3,0,1,3,2,1,3,4,1
50030 REM WHITE
50030 DATA 3,1,15,3,3,15,3,5,15
50040 REM PLAYER LEFT DIRECTION
50040 REM BLACK
50040 DATA 3,6,1,3,8,1,3,10,1
50060 REM WHITE
50060 DATA 3,7,15,3,9,15,3,11,15
50080 REM FOX - RIGHT
50080 REM BLACK
50080 DATA 2,12,1,2,14,1,2,16,1
50100 REM YELLOW
50100 DATA 2,13,11,2,15,11,2,17,11
50110 REM FOX - LEFT
50110 REM BLACK
50110 DATA 2,18,1,2,20,1,2,22,1
50120 REM YELLOW
50120 DATA 2,19,11,2,21,11,2,23,11
50130 REM SNAKE - RIGHT
50130 REM BLACK
50130 DATA 2,24,1,2,26,1
50140 REM MAGENTA
50140 DATA 2,25,13,2,27,13
50150 REM SNAKE - LEFT
50150 REM BLACK
50150 DATA 2,28,1,2,30,1
50160 REM MAGENTA
50160 DATA 2,29,13,2,31,13

50500 REM PATTERN DEFINITION CHANGE ANIMATIONS - NUMBER
50500 DATA 2
50510 REM ANIMATION - DEFINITONS (DURATION, POINTER)
50510 REM PLATFORM
50510 DATA 10,&H7000,10,&H7008

51000 REM ANIMATION DEFINITIONS (SIZE,ANIMATION ITEMS,...)
51000 REM TOTAL NUMBER
51000 DATA 13
51010 REM PLAYER - RIGHT BLACK
51010 DATA 4,0,1,2,1
51030 REM PLAYER - RIGHT WHITE
51030 DATA 4,3,4,5,4
51040 REM PLAYER - LEFT BLACK
51040 DATA 4,6,7,8,7
51060 REM PLAYER - LEFT WHITE
51060 DATA 4,9,10,11,10
51080 REM FOX - RIGHT BLACK
51080 DATA 4,12,13,14,13
51090 REM FOX - RIGHT YELLOW
51090 DATA 4,15,16,17,16
51100 REM FOX - LEFT BLACK
51100 DATA 4,18,19,20,19
51110 REM FOX - LEFT YELLOW
51110 DATA 4,21,22,23,22
51120 REM SNAKE - RIGHT BLACK
51120 DATA 2,24,25
51130 REM SNAKE - RIGHT MAGENTA
51130 DATA 2,26,27
51140 REM SNAKE - LEFT BLACK
51140 DATA 2,28,29
51150 REM SNAKE - LEFT MAGENTA
51150 DATA 2,30,31
51160 REM PLATFORM
51160 DATA 2,32,33

52000 REM SPRITE ANIMATIONS (SPRITE NUMBER, ANIMATION DEFINITION, CYCLIC FLAG)
52000 REM TOTAL NUMBER
52000 DATA 16
52010 REM PLAYER ANIMATION
52010 REM SPRITE ANIMATION RIGHT BLACK
52010 DATA 0,0,1
52030 REM SPRITE ANIMATION RIGHT WHITE
52030 DATA 1,1,1
52040 REM SPRITE ANIMATION LEFT BLACK
52040 DATA 0,2,1
52060 REM SPRITE ANIMATION LEFT WHITE
52060 DATA 1,3,1
52080 REM FOX 1 ANIMATION
52080 REM SPRITE ANIMATION RIGHT BLACK
52080 DATA 2,4,1
52090 REM SPRITE ANIMATION RIGHT YELLOW
52090 DATA 3,5,1
52100 REM SPRITE ANIMATION LEFT BLACK
52100 DATA 2,6,1
52110 REM SPRITE ANIMATION LEFT YELLOW
52110 DATA 3,7,1
52120 REM FOX 2 ANIMATION
52120 REM SPRITE ANIMATION RIGHT BLACK
52120 DATA 4,4,1
52130 REM SPRITE ANIMATION RIGHT YELLOW
52130 DATA 5,5,1
52140 REM SPRITE ANIMATION LEFT BLACK
52140 DATA 4,6,1
52150 REM SPRITE ANIMATION LEFT YELLOW
52150 DATA 5,7,1
52160 REM SNAKE 1 ANIMATION
52160 REM SPRITE ANIMATION RIGHT BLACK
52160 DATA 6,8,1
52170 REM SPRITE ANIMATION RIGHT MAGENTA
52170 DATA 7,9,1
52180 REM SPRITE ANIMATION LEFT BLACK
52180 DATA 6,10,1
52190 REM SPRITE ANIMATION LEFT MAGENTA
52190 DATA 7,11,1

53000 REM CHARACTER ANIMATIONS (CHARACTER NUMBER, ANIMATION DEFINITION, CYCLIC FLAG)
53000 REM TOTAL NUMBER
53000 DATA 3
53010 REM PLATFORM
53010 DATA 255,12,1
53020 DATA 511,12,1
53030 DATA 767,12,1