10 REM SPRITE HANDLING TEST
20 COLOR 15,1,1:SCREEN 2,2
25 CLEAR 300,&HC000:DEFINT A-Z
30 REM DEFINE ALL VARIABLES USED
31 REM THIS PREVENTS VARIABLES 
32 REM CHANGING MEMORY LOCATION
40 J=0:I=0:X=0:Y=0:VX=1:VY=1:SU=0
41 REM DEFINE SPRITE ATRIBUTE TABLE 
42 REM FOR 32 SPRITES IN FORMAT 
43 REM (Y,X,PATTERN,COLOR)
44 DIM SA(3,31)
45 REM DEFINE SPRITE GROUP IN FORMAT 
46 REM (SPRITE NUM, DELTA Y, DELTA X)
47 DIM SG(2,31)
49 REM INIT SPRITE ROUTINES
50 SU=0:REM SPRITE UPDATE FLAG
51 _SPRENABLE (VARPTR(SA(0,0)),VARPTR(SU),1)
52 ON STOP GOSUB 2000:STOP ON
58 REM SET INITIAL POSITIONS, 
59 REM PATTERNS AND COLORS
60 FOR I=0 TO 31
70 _SPRSET (I,0,208,I,4)
80 NEXT
89 REM DEFINE RELATIVE POSITIONS OF 
89 REM SPRITES IN A GROUP
90 FOR I=0 TO 7:FOR J=0 TO 3
91 REM SET SPRITE NUMBER
92 SG(0,I*4+J)=I*4+J
93 REM SET Y OFFSET
94 SG(1,I*4+J)=I*16
95 REM SET X OFFSET
96 SG(2,I*4+J)=J*16
105 NEXT J:NEXT I
110 BLOAD "MSXLOGOV.BIN",&HC001
120 _MEMVRM(&HC001,BASE(14),32*32,0)
130 _SPRGRPMOV (X,Y,VARPTR(SG(0,0)),32)
180 SU=1:GOSUB 1000
190 IF SU=1 GOTO 190 ELSE 130
1000 X=X+VX:IF X<0 OR X>192 THEN VX=-VX
1010 Y=Y+VY:IF Y<0 OR Y>64 THEN VY=-VY
1020 RETURN
2000 _SPRDISABLE:END


