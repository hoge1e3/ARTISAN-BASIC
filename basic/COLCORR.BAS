10 DEFINT A-Z:COLOR 15,1,1:SCREEN 2,2,0
20 BLOAD"FOREST.SC2",S
25 REM DEFINE POINTER SPRITE 10x3
30 S$="":FOR A=0 TO 31:READ X:S$=S$+CHR$(X):NEXT A  
40 SPRITE$(0)=S$
50 A=PEEK(4)+256*PEEK(5)+384
55 REM DEFINE SPRITES FOR NUMBERS 0-9
60 FOR X=0 TO 9
70 S$="":FOR Y=0 TO 7
80 C=PEEK(A+X*8+Y):S$=S$+CHR$(C):NEXT Y
90 FOR Y=0 TO 23:S$=S$+CHR$(0):NEXT Y
100 SPRITE$(X+1)=S$:NEXT X
105 REM DEFINE SPRITES FOR HEX NUMBERS A-F
110 A=PEEK(4)+256*PEEK(5)+520
120 FOR X=0 TO 5
130 S$="":FOR Y=0 TO 7
140 C=PEEK(A+X*8+Y):S$=S$+CHR$(C):NEXT Y
150 FOR Y=0 TO 23:S$=S$+CHR$(0):NEXT Y
160 SPRITE$(X+11)=S$:NEXT X
170 REM DEFINE SPRITE FOR BACKGROUND
180 S$="":FOR Y=0 TO 31:S$=S$+CHR$(255):NEXT Y:SPRITE$(17)=S$
500 REM MAIN LOOP
501 REM A - ANALYZE AND MARK POSITIONS WITH SAME FORE AND BACK COLOUR
502 REM CURSORS - MOVE POINTER  
502 REM SPACE SET COLOR
503 REM S - SET NEW COLOR FOR APPLICATION
510 X=0:Y=0:GOSUB 11000:GOSUB 12000
520 S$=INKEY$
530 IF S$="" THEN GOSUB 11000:GOTO 520
540 IF S$="A" OR S$="a" GOTO 1000
550 IF S$<>CHR$(28) GOTO 580
560 X=X+1:IF X>31 THEN X=0:Y=(Y+1) MOD 192
570 GOSUB 11000:GOSUB 12000:GOTO 520
580 IF S$<>CHR$(29) GOTO 610
590 X=X-1:IF X<0 THEN X=31:Y=Y-1:IF Y<0 THEN Y=191
600 GOTO 570
610 IF S$<>CHR$(30) GOTO 640
620 Y=Y-1:IF Y<0 THEN Y=191
630 GOTO 570
640 IF S$<>CHR$(31) GOTO 800
650 Y=(Y+1) MOD 192
660 GOTO 570
800 GOTO 520
999 REM INITIAL RECOLOUR - BRIGHTER TO FOREGROUND
1000 FOR Y=0 TO 191
1010 FOR X=0 TO 31
1015 GOSUB 11000
1020 A=(Y\8)*256+X*8+(Y MOD 8):PA=A+BASE(12):CA=A+BASE(11)
1030 P=VPEEK(PA):C=VPEEK(CA)
1040 FC=C\16:BC=C MOD 16
1050 IF BC=FC THEN VPOKE CA,255:VPOKE PA,0:GOTO 1500
1060 IF FC=1 OR FC=0 GOTO 1400 
1390 GOTO 1500
1400 VPOKE CA,BC*16+FC:VPOKE PA,P XOR 255
1500 NEXT X:NEXT Y
10000 GOTO 510
11000 PUT SPRITE 0,(X*8-1,Y-2),INT(RND(1)*15)+1,0:RETURN
12000 K=VPEEK((Y\8)*256+X*8+(Y MOD 8)+BASE(11))
12010 FK=K\16:BK=K MOD 16
12020 IF X>=30 AND Y>=184 THEN KX=0 ELSE KX=240
12030 PUT SPRITE 31,(KX,183),1,17
12040 PUT SPRITE 1,(KX+1,183),15,FK+1
12050 PUT SPRITE 2,(KX+9,183),15,BK+1
12060 RETURN
20000 DATA 255,128,255,0,0,0,0,0
20010 DATA 0,0,0,0,0,0,0,0
20020 DATA 192,64,192,0,0,0,0,0
20030 DATA 0,0,0,0,0,0,0,0
